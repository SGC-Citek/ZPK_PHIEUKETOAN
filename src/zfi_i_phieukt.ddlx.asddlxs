@Metadata.layer: #CORE
@UI: {
       headerInfo: {
            typeName: 'JournalEntry',
            typeNamePlural: 'Phiếu Kế Toán',
            title: {
    type: #STANDARD,
    label: 'Journal'
            }
       },
    presentationVariant: [{ sortOrder: [{ by: 'AccountingDocument', direction: #ASC}] }]
}
annotate view ZFI_I_PHIEUKT with
  parameters
    @EndUserText.label: 'Own Document Only'
    @Consumption.defaultValue: 'X'
    OwnDocumentOnly
{
  @UI.facet: [ { id:              'Header',
              purpose:         #STANDARD,
              type:            #IDENTIFICATION_REFERENCE,
              label:           'Header',
              position:        10 },
            { id:              'Item',
              purpose:         #STANDARD,
              type:            #LINEITEM_REFERENCE,
              label:           'Item',
              position:        20,
              targetElement:   '_Item'}]

  @UI: { lineItem: [{ position: 10 }], /*define colummn*/
         identification: [{ position: 10 }],/*show fields when click on the item*/
         selectionField: [{ position: 10 }],/*define selection screen*/
         textArrangement: #TEXT_FIRST }
  @Consumption.filter: {selectionType: #SINGLE, mandatory: true, defaultValue: '1000' }
  CompanyCode;

  @UI: { lineItem: [{ position: 20 }],
         identification: [{ position: 20 }],
         selectionField: [{ position: 20 }]
         }
  @Consumption.filter: {mandatory: true}
  FiscalYear;

  @UI: { lineItem: [{ position: 40 }],
         identification: [{ position: 40 }],
         selectionField: [{ position: 40 }] }
  @UI.multiLineText: true
  @Consumption.filter: {multipleSelections: true, selectionType: #RANGE}
  @Consumption.valueHelpDefinition: [{ entity: {
      name: 'ZFI_I_JOURNALENTRY',
      element: 'AccountingDocument'
  } }]
  @EndUserText.label: 'Journal Entry'
  AccountingDocument;

  @UI: { lineItem: [{ position: 30 }],
        identification: [{ position: 30 }],
        selectionField: [{ position: 30 }] }
  @Consumption.filter: { multipleSelections: true, selectionType:  #RANGE}
  @EndUserText.label: 'Journal Entry Type'
  AccountingDocumentType;

  @UI: { lineItem: [{ position: 50 }],
        identification: [{ position: 60 }],
         selectionField: [{ position: 50 }] }
  @Consumption.filter.selectionType: #SINGLE
  @EndUserText.label: 'Reference'
  DocumentReferenceID;

  @UI: { lineItem: [{ position: 60 }],
         identification: [{ position: 60 }]}
  @EndUserText.label: 'Accounting Doc Created By User'
  AccountingDocCreatedByUser;

  //  @UI: { lineItem: [{ position: 70 }],
  //         identification: [{ position: 70 }]}
  //  @EndUserText.label: 'Accounting Document Creation Date'
  //  AccountingDocumentCreationDate;

  @UI: { lineItem: [{ position: 80 }],
         identification: [{ position: 80 }],
         selectionField: [{ position: 80 }] }
  @EndUserText.label: 'Document Date'
  DocumentDate;

  @UI: { lineItem: [{ position: 90 }],
         identification: [{ position: 90 }],
         selectionField: [{ position: 90 }] }
  @EndUserText.label: 'Posting Date'
  PostingDate;

  @UI: { lineItem: [{ position: 100 }],
         identification: [{ position: 100 }]}
  @EndUserText.label: 'Nội dung'
  noi_dung;

  @UI: { lineItem: [{ position: 110 }],
         identification: [{ position: 110 }]}
  @EndUserText.label: 'Amount'

  amount;
}